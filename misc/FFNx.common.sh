/****************************************************************************/
//    Copyright (C) 2023 Cosmos                                             //
//                                                                          //
//    This file is part of FFNx                                             //
//                                                                          //
//    FFNx is free software: you can redistribute it and/or modify          //
//    it under the terms of the GNU General Public License as published by  //
//    the Free Software Foundation, either version 3 of the License         //
//                                                                          //
//    FFNx is distributed in the hope that it will be useful,               //
//    but WITHOUT ANY WARRANTY; without even the implied warranty of        //
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         //
//    GNU General Public License for more details.                          //
/****************************************************************************/

// Constants for BT1886 Appendix 1 EOTF Function
// These constants correspond to a mid-90s Sony Trinitron CRT with the brightness turned pretty far up and the contrast turned pretty far down.
// Do not change them blindly. If you change black or white level, then B, K, S, and I need to be recalculated.
// And LUTs will need to be recalculated.
// (https://github.com/ChthonVII/gamutthingy can calculate them for you.)
#define crtBlackLevel 0.0018
#define crtWhiteLevel 1.5
#define crtConstantB 0.1042361441620798
#define crtConstantK 1.1591247176412838
#define crtConstantS 1.3711574400867077
#define crtConstantI 0.1489575626482313

// Gamut LUT
SAMPLER2D(tex_10, 10);

// YUV to RGB ---------------------------------------------------------
// tv-range functions include implicit range expansion

vec3 toRGB_bt601_fullrange(vec3 yuv_input)
{
	mat3 jpeg_rgb_transform = mtxFromCols(
		vec3(+1.000, +1.000, +1.000),
		vec3(+0.000, -0.202008 / 0.587, +1.772),
		vec3(+1.402, -0.419198 / 0.587, +0.000)
	);
	return saturate(mul(jpeg_rgb_transform, yuv_input));
}

vec3 toRGB_bt709_fullrange(vec3 yuv_input)
{
	mat3 bt709full_rgb_transform = mtxFromCols(
		vec3(+1.000, +1.000, +1.000),
		vec3(+0.000, -0.13397432 / 0.7152, +1.8556),
		vec3(+1.5748, -0.33480248 / 0.7152 , +0.000)
	);
	return saturate(mul(bt709full_rgb_transform, yuv_input));
}

vec3 toRGB_bink_fullrange(vec3 yuv_input)
{
	mat3 bink_yuv_rgb_transform = mtxFromCols(
		vec3(+0.992121226065255, +1.00472469291526, +0.993728379556196),
		vec3(-0.00771162997168706, -0.714434200767644, +1.40552508918856),
		vec3(+1.77255699135453, -0.343897012250908, -0.000682109776365476)
	);
	return saturate(mul(bink_yuv_rgb_transform, yuv_input));
}

// Gamma functions ------------------------------------------------

// gamma encoded --> linear:

// sRGB
vec3 toLinear(vec3 _rgb)
{
	bvec3 cutoff = lessThan(_rgb.rgb, vec3_splat(0.04045));
	vec3 higher = pow((_rgb.rgb + vec3_splat(0.055)) / vec3_splat(1.055), vec3_splat(2.4));
	vec3 lower = _rgb.rgb / vec3_splat(12.92);

	return saturate(mix(higher, lower, cutoff));
}

vec3 toLinearSMPTE170M(vec3 _rgb)
{
	bvec3 cutoff = lessThan(_rgb.rgb, vec3_splat(0.0812));
	vec3 higher = pow((_rgb.rgb + vec3_splat(0.099)) / vec3_splat(1.099), (vec3_splat(1.0) / vec3_splat(0.45)));
	vec3 lower = _rgb.rgb / vec3_splat(4.5);

	return saturate(mix(higher, lower, cutoff));
}

vec3 toLinear2pt2(vec3 _rgb)
{
	return saturate(pow(_rgb.rgb, vec3_splat(2.2)));
}

// Microsoft says PAL uses a pure 2.8 gamma curve. See: https://learn.microsoft.com/en-us/windows/win32/api/mfobjects/ne-mfobjects-mfvideotransferfunction
// ffmpeg thinks there *should* be a linear toe slope, but uses a pure curve since they cannot find any documentation for it. See: https://github.com/FFmpeg/FFmpeg/blob/master/libavfilter/vf_colorspace.c#L162
// In any event, Poynton says 2.8 is "unrealistically high" and PAL CRT units did not really behave like that.
// They had the same gamma function as every other CRT (reasonably well approximated by BT1886 Appendix 1), because the underlying physics so dictated.
// Notwithstanding the behavior of the CRTs, digital video encoded for PAL/EBU might have followed the 2.8 gamma in the spec.
// PAL switched to the SMPTE170M function in 2005 (see BT1700)
vec3 toLinear2pt8(vec3 _rgb)
{
	return saturate(pow(_rgb.rgb, vec3_splat(2.8)));
}

// EOTF Function from BT1886 Appendix 1 (https://www.itu.int/dms_pubrec/itu-r/rec/bt/R-REC-BT.1886-0-201103-I!!PDF-E.pdf)
// Approximates the gamma behavior of a CRT (more accurately than the crummy Annex 1 function)
// Constants have been selected to match a mid-90s Sony Trinitron CRT with the brightness turned pretty far up and the contrast turned pretty far down.
// Assumes input in range 0-1, output in range 0-1.
vec3 toLinearBT1886Appx1Fast(vec3 _rgb)
{
	// add B
	_rgb = _rgb + vec3_splat(crtConstantB);
	// EOTF
	bvec3 cutoff = lessThan(_rgb.rgb, vec3_splat(0.35 + crtConstantB));
	vec3 higher = pow(_rgb, vec3_splat(2.6)) * vec3_splat(crtConstantK);
	vec3 lower = pow(_rgb, vec3_splat(3.0)) * vec3_splat(crtConstantK) * vec3_splat(crtConstantS);
	vec3 outcolor = mix(higher, lower, cutoff);
	// renormalize
	outcolor = outcolor - vec3_splat(crtBlackLevel);
	outcolor = outcolor / vec3_splat(crtWhiteLevel - crtBlackLevel);
	return saturate(outcolor);
}

// linear --> gamma encoded:

// sRGB
vec3 toGamma(vec3 _rgb)
{
	bvec3 cutoff = lessThan(_rgb.rgb, vec3_splat(0.0031308));
	vec3 higher = vec3_splat(1.055) * pow(_rgb.rgb, vec3_splat(1.0/2.4)) - vec3_splat(0.055);
	vec3 lower = _rgb.rgb * vec3_splat(12.92);

	return saturate(mix(higher, lower, cutoff));
}

// See https://github.com/Microsoft/DirectX-Graphics-Samples/blob/master/MiniEngine/Core/Shaders/ColorSpaceUtility.hlsli#L75
// max_nits should be "the brightness level that SDR 'white' is rendered at within an HDR monitor" (probably 100-200ish)
// Google Chrome uses a default of 200 if autodetection fails.
vec3 ApplyREC2084Curve(vec3 _color, float max_nits)
{
	// reference PQ OETF will yield reference OOTF when
	// displayed on  a reference monitor employing EOTF

	float m1 = 2610.0 / 4096.0 * 1.0 / 4;
	float m2 = 2523.0 / 4096.0 * 128;
	float c1 = 3424.0 / 4096.0;
	float c2 = 2413.0 / 4096.0 * 32;
	float c3 = 2392.0 / 4096.0 * 32;

	vec3 Lp = pow(_color * (vec3_splat(max_nits)/vec3_splat(10000.0)), vec3_splat(m1));
	return saturate(pow((c1 + c2 * Lp) / (vec3_splat(1.0) + c3 * Lp), vec3_splat(m2)));
}

// Inverse EOTF Function from BT1886 Appendix 1 (https://www.itu.int/dms_pubrec/itu-r/rec/bt/R-REC-BT.1886-0-201103-I!!PDF-E.pdf)
// Approximates the inverse of gamma behavior of a CRT (more accurately than the crummy Annex 1 function)
// Constants have been selected to match a mid-90s Sony Trinitron CRT with the brightness turned pretty far up.
// Not used - retained for posterity, possible use in future
/*
vec3 toGammaBT1886Appx1Fast(vec3 _rgb)
{
	// undo the chop and normalization post-processing
	_rgb = _rgb * vec3_splat(crtWhiteLevel - crtBlackLevel);
	_rgb = _rgb + vec3_splat(crtBlackLevel);

	// Inverse EOTF
	bvec3 cutoff = lessThan(_rgb.rgb, vec3_splat(crtConstantI));
	vec3 higher = pow(vec3_splat(1.0/crtConstantK) * _rgb, vec3_splat(1.0/2.6));
	vec3 lower = pow(vec3_splat(1.0/crtConstantK) * vec3_splat(1.0/crtConstantS) * _rgb, vec3_splat(1.0/3.0));
	vec3 outcolor = mix(higher, lower, cutoff);

	//unshift
	outcolor = outcolor - vec3_splat(crtConstantB);

	return saturate(outcolor);
}
*/

// Gamut conversions ---------------------------------------------
// For NTSC-J:
// 		Start from gamma-space R'G'B'
// 		Simulate color correction circuit
// 		Linearize with BT1886 Appendix 1 EOTF function
// 		Chromatic adaption to convert whitepoint to D65
// 		Gamut conversion to sRGB
// 		Gamut compression to make result fit inside sRGB gamut
// 		Output linear RGB in sRGB gamut
// For SMPTEC/EBU movie:
// 		Do linear-to-linear gamut conversion + compression.

// Note: sRGB is the same gamut as rec709 video.

// Note: High precision values are used for the "D65" whitepoint. (x=0.312713, y=0.329016)

// To rec2020:
// See https://github.com/Microsoft/DirectX-Graphics-Samples/blob/master/MiniEngine/Core/Shaders/ColorSpaceUtility.hlsli#L120
vec3 convertGamut_SRGBtoREC2020(vec3 rgb_input)
{
	mat3 toRec2020 = mtxFromCols(
		vec3(+0.6274191906, +0.0691004930, +0.0163944170),
		vec3(+0.3292752691, +0.9195389158, +0.0880250760),
		vec3(+0.0433055403, +0.0113605912, +0.8955805070)
	);
	return saturate(mul(toRec2020, rgb_input));
}

// NTSC-J (actually P22) to REC2020 (linear RGB input, linear RGB output)
// Converts P22 primaries (gamutthingy's "P22_trinitron_mixandmatch" with 9300K+8MPCD whitepoint to REC2020 primaries with D65 whitepoint)
// (flip matrix b/c shader languages are column-major)
vec3 convertGamut_NTSCJtoREC2020(vec3 rgb_input)
{
	mat3 NTSCJtoRec2020 = mtxFromCols(
		vec3(+0.6117882897, +0.0797156500, +0.0072571191),
		vec3(+0.3142775896, +0.8673198799, +0.0632222078),
		vec3(+0.0739341207, +0.0529644702, +0.9295206731)
	);
	// clamp low in case of floating point errors; don't clamp high
	// out-of-bounds red will get scaled down to something sane inside ApplyREC2084Curve()
	// end result should be a pure red that's stronger than the red component of white (but still easily in bounds for rec2020)
	return max(mul(NTSCJtoRec2020, rgb_input), vec3_splat(0.0));
}


// Simulate CRT behavior (gamma-space R'G'B' input, linear RGB output)
// Some outputs **WILL** be > 1.0! Make sure subsequent code can handle that.
vec3 CRTSimulation(vec3 rgb_input){
	// Apply the color correction matrix
	// Matrices can be derived from the chip datasheets through the inverse of the method explained in
	// Parker, Norman W. "An Analysis of the Necessary Decoder Corrections for Color Receiver Operation with Non-Standard Receiver Primaries." IEEE Transactions on Consumer Electronics, Vol. CE-28, No. 1, pp. 74-83. February 1982. (Reprint of 1966 original.)
	// This matrix corresponds to the CXA2060BS in JP mode.
	// With the saturation knob turned up a bit, also rolled into the matrix.
	// (flip matrix b/c shader languages are column-major)
	mat3 crtMatrix = mtxFromCols(
		vec3(+1.3302672030, +0.0020136150, -0.0045265039),
		vec3(-0.2173088529, +0.9972330662, -0.0233075243),
		vec3(-0.1129583501, +0.0007533188, +1.0278340282)
	);
	vec3 corrected = mul(crtMatrix, rgb_input);

	// It does not appear that CRTs low clamped the output from color correction.
	// But it was implicitly clamped by the fact electron guns don't run in reverse.

	// Whether CRTs ever high clamped the output from color correction (and, if so, how high) is a great mystery.
	// The scant evidence available suggests no clamping at all.
	// But that could be wrong, or it could vary by model.

	// linearize with a slightly modified version of BT1886 that accepts out-of-bounds input
	// (which we need due to not clamping the color correction circuit)
	// add B
	corrected = corrected + vec3_splat(crtConstantB);
	// clamp low at zero light emission (electron gun doesn't go in reverse)
	corrected = max(corrected, vec3_splat(0.0));
	// EOTF
	bvec3 cutoff = lessThan(corrected.rgb, vec3_splat(0.35 + crtConstantB));
	vec3 higher = pow(corrected, vec3_splat(2.6)) * vec3_splat(crtConstantK);
	vec3 lower = pow(corrected, vec3_splat(3.0)) * vec3_splat(crtConstantK) * vec3_splat(crtConstantS);
	vec3 outcolor = mix(higher, lower, cutoff);
	// renormalize
	outcolor = outcolor - vec3_splat(crtBlackLevel);
	outcolor = outcolor / vec3_splat(crtWhiteLevel - crtBlackLevel);
	// clamp low in case of floating point errors; don't clamp high
	return max(outcolor, vec3_splat(0.0));
}

// Inverse of CRTSimulation()
// Accepts inputs >1.0; output is clamped 0-1.
vec3 InverseCRTSimulation(vec3 rgb_input){

	///// start by inverting gamma function

	// undo the chop and normalization post-processing
	rgb_input = rgb_input * vec3_splat(crtWhiteLevel - crtBlackLevel);
	rgb_input = rgb_input + vec3_splat(crtBlackLevel);

	// clamp any value that's still negative
	rgb_input = max(rgb_input, vec3_splat(0.0));

	// Inverse EOTF
	bvec3 cutoff = lessThan(rgb_input.rgb, vec3_splat(crtConstantI));
	vec3 higher = pow(vec3_splat(1.0/crtConstantK) * rgb_input, vec3_splat(1.0/2.6));
	vec3 lower = pow(vec3_splat(1.0/crtConstantK) * vec3_splat(1.0/crtConstantS) * rgb_input, vec3_splat(1.0/3.0));
	vec3 outcolor = mix(higher, lower, cutoff);

	//unshift
	outcolor = outcolor - vec3_splat(crtConstantB);

	//// apply inverse of color correction matrix
	mat3 inverseCRTMatrix = mtxFromCols(
		vec3(+0.7517584969, -0.0015204271, +0.0032762102),
		vec3(+0.1657451679, +1.0024222152, +0.0234612064),
		vec3(+0.0824963352, -0.0009017881, +0.9732625834)
	);
	outcolor = mul(inverseCRTMatrix, outcolor);

	//// clamp
	// strictly clamp to 0-1 because this corresponds to the console's R'G'B' output, which is defined by integer math
	// also, clamping here is a substitute for clamping linear RGB operations we couldn't clamp earlier due to CRTSimulation()'s outputs >1.0.
	return saturate(outcolor);
}

// Apply the inverse of the CRT color correction matrix. (gamma-space R'G'B' input and output)
// When used together with NTSC-J mode, the overall result is what you'd get from a CRT television
// with P22 phosphors, 9300K+8MPCD whitepoint, and no color correction circuit --
// which basically describes a mid-90s CRT computer monitor.
vec3 CRTUncorrect(vec3 rgb_input){
	//// apply inverse of color correction matrix
	mat3 inverseCRTMatrix = mtxFromCols(
		vec3(+0.7517584969, -0.0015204271, +0.0032762102),
		vec3(+0.1657451679, +1.0024222152, +0.0234612064),
		vec3(+0.0824963352, -0.0009017881, +0.9732625834)
	);
	vec3 outcolor = mul(inverseCRTMatrix, rgb_input);
	// clamp
	return saturate(outcolor);
}

// This is a generic 3D LUT function.
// We're using it to do gamut conversions when a gamut compression mapping algorithm is necessary to avoid losing detail to clipping.
// Since that's waaaay too compute heavy, we precompute it, then use a LUT.
// Renderer::AssignGamutLUT() in renderer.cpp is in charge of making sure the correct LUT is bound.
// Expects:
// - coords 0,0 in the upper left corner
// - 4096x64 dimensions (FYI: BGFX cannot handle 16384x128)
// - Stores values as gamma-space sRGB. (They will be linearized.)
// - black in the upper left corner
// - green on the vertical axis
// - red on the small horizontal axis
// - blue on the large horizontal axis
// - input is CRT gamma-space R'G'B' (before the CRT's color correction circuit)
// - output is always linearized
vec3 GamutLUT(vec3 rgb_input)
{
	vec3 temp = saturate(rgb_input) * vec3_splat(63.0); // use 63 b/c LUT is created with left-of-bin DAC to make interpolation here easier
	vec3 floors = floor(temp);
	vec3 ceils = ceil(temp);
	// If the input is in CRT gamma space, we need to get it, and the LUT indices, into linear RGB to compute interpolation weights.
	// This two-corner method is slightly wrong because the gamma-space indices don't form a perfect cube in linear space.
	// But it should be close enough that the error is smaller than quantization error.
	vec3 temp_linear = CRTSimulation(rgb_input);
	vec3 floors_linear = CRTSimulation(floors/vec3_splat(63.0));
	vec3 ceils_linear = CRTSimulation(ceils/vec3_splat(63.0));
	vec3 ceilweights = saturate((temp_linear - floors_linear) / max((ceils_linear - floors_linear), vec3_splat(0.0000000001))); // avoid div0

	// multiply blue by 64, as it is the horizontal "major" axis
	floors = floors * vec3(1.0, 1.0, 64.0);
	ceils = ceils * vec3(1.0, 1.0, 64.0);

	// take 8 samples
	// LUT is always stored in gamma-space sRGB, so linearize them
	vec3 RfGfBf = toLinear(texelFetch(tex_10, ivec2(floors.b + floors.r, floors.g), 0).xyz);
	vec3 RfGfBc = toLinear(texelFetch(tex_10, ivec2(ceils.b + floors.r, floors.g), 0).xyz);
	vec3 RfGcBf = toLinear(texelFetch(tex_10, ivec2(floors.b + floors.r, ceils.g), 0).xyz);
	vec3 RfGcBc = toLinear(texelFetch(tex_10, ivec2(ceils.b + floors.r, ceils.g), 0).xyz);
	vec3 RcGfBf = toLinear(texelFetch(tex_10, ivec2(floors.b + ceils.r, floors.g), 0).xyz);
	vec3 RcGfBc = toLinear(texelFetch(tex_10, ivec2(ceils.b + ceils.r, floors.g), 0).xyz);
	vec3 RcGcBf = toLinear(texelFetch(tex_10, ivec2(floors.b + ceils.r, ceils.g), 0).xyz);
	vec3 RcGcBc = toLinear(texelFetch(tex_10, ivec2(ceils.b + ceils.r, ceils.g), 0).xyz);

	// merge down to 4 samples along blue axis
	vec3 RfGf = mix(RfGfBf, RfGfBc, vec3_splat(ceilweights.b));
	vec3 RfGc = mix(RfGcBf, RfGcBc, vec3_splat(ceilweights.b));
	vec3 RcGf = mix(RcGfBf, RcGfBc, vec3_splat(ceilweights.b));
	vec3 RcGc = mix(RcGcBf, RcGcBc, vec3_splat(ceilweights.b));

	// merge down to 2 samples along green axis
	vec3 Rf = mix(RfGf, RfGc, vec3_splat(ceilweights.g));
	vec3 Rc = mix(RcGf, RcGc, vec3_splat(ceilweights.g));

	// merge down to one color along red axis
	vec3 outcolor = mix(Rf, Rc, vec3_splat(ceilweights.r));

	return saturate(outcolor);
}

// This is the same as GamutLUT() except that:
//   - The input is linear RGB
//   - The output is CRT gamma-space R'G'B' (before the CRT's color correction circuit)
//   - The stored values are CRT gamma-space R'G'B' (before the CRT's color correction circuit)
vec3 GamutLUTBackwards(vec3 rgb_input)
{
	vec3 temp = saturate(rgb_input) * vec3_splat(63.0); // use 63 b/c LUT is created with left-of-bin DAC to make interpolation here easier
	vec3 floors = floor(temp);
	vec3 ceils = ceil(temp);
	vec3 ceilweights = saturate(temp - floors);

	// multiply blue by 64, as it is the horizontal "major" axis
	floors = floors * vec3(1.0, 1.0, 64.0);
	ceils = ceils * vec3(1.0, 1.0, 64.0);

	// take 8 samples
	// LUT is always stored as CRT gamma-space R'G'B', so run CRT simulation to linearize them
	vec3 RfGfBf = CRTSimulation(texelFetch(tex_10, ivec2(floors.b + floors.r, floors.g), 0).xyz);
	vec3 RfGfBc = CRTSimulation(texelFetch(tex_10, ivec2(ceils.b + floors.r, floors.g), 0).xyz);
	vec3 RfGcBf = CRTSimulation(texelFetch(tex_10, ivec2(floors.b + floors.r, ceils.g), 0).xyz);
	vec3 RfGcBc = CRTSimulation(texelFetch(tex_10, ivec2(ceils.b + floors.r, ceils.g), 0).xyz);
	vec3 RcGfBf = CRTSimulation(texelFetch(tex_10, ivec2(floors.b + ceils.r, floors.g), 0).xyz);
	vec3 RcGfBc = CRTSimulation(texelFetch(tex_10, ivec2(ceils.b + ceils.r, floors.g), 0).xyz);
	vec3 RcGcBf = CRTSimulation(texelFetch(tex_10, ivec2(floors.b + ceils.r, ceils.g), 0).xyz);
	vec3 RcGcBc = CRTSimulation(texelFetch(tex_10, ivec2(ceils.b + ceils.r, ceils.g), 0).xyz);

	// merge down to 4 samples along blue axis
	vec3 RfGf = mix(RfGfBf, RfGfBc, vec3_splat(ceilweights.b));
	vec3 RfGc = mix(RfGcBf, RfGcBc, vec3_splat(ceilweights.b));
	vec3 RcGf = mix(RcGfBf, RcGfBc, vec3_splat(ceilweights.b));
	vec3 RcGc = mix(RcGcBf, RcGcBc, vec3_splat(ceilweights.b));

	// merge down to 2 samples along green axis
	vec3 Rf = mix(RfGf, RfGc, vec3_splat(ceilweights.g));
	vec3 Rc = mix(RcGf, RcGc, vec3_splat(ceilweights.g));

	// merge down to one color along red axis
	vec3 outcolor = mix(Rf, Rc, vec3_splat(ceilweights.r));

	return InverseCRTSimulation(outcolor);
}

// Dithering ---------------------------------------------

// Apply Martin Roberts' quasirandom dithering scaled below a specified level of precision.
// See https://extremelearning.com.au/unreasonable-effectiveness-of-quasirandom-sequences/
// pixelval: float (range 0-1) pixel color trio.
// coords: float (range 0-1) pixel coordinates, i.e., v_texcoord0.xy
// ydims: integer dimensions of first channel's texture
// udims & vdims: integer dimensions of first second and third channels' textures (may differ from ydim for various yuv formats)
// scale_divisor: step size divisor to scale the dithering to fit within. E.g., use 256.0 for dithering 8-bit values.
// xyoffset: value to add to x & y coords. Should be at least 1 to avoid x=0 and y=0. Should be different if the same input is dithered twice.
// (This function will be used twice if TV-range video is dithered for range expansion, then again for HDR bit depth increase and/or gamut conversion.)
vec3 QuasirandomDither(vec3 pixelval, vec2 coords, ivec2 ydims, ivec2 udims, ivec2 vdims, float scale_divisor, float xyoffset)
{
	// get integer range x,y coords for this pixel
	// invert one axis for u and the other axis for v to decouple dither patterns across channels
	// see https://blog.kaetemi.be/2015/04/01/practical-bayer-dithering/
	// add 1 to avoid x=0 and y=0
	vec3 xpos = vec3(
		round(float(ydims.x) * coords.x)  + xyoffset,
		round(float(udims.x) * (1.0 - coords.x)) + xyoffset,
		round(float(vdims.x) * coords.x)  + xyoffset
	);
	vec3 ypos = vec3(
		round(float(ydims.y) * coords.y) + xyoffset,
		round(float(udims.y) * coords.y) + xyoffset,
		round(float(vdims.y) * (1.0 - coords.y)) + xyoffset
	);
	// R series magic
	vec3 dither = fract((xpos * vec3_splat(0.7548776662)) + (ypos * vec3_splat(0.56984029)));
	// triangular wave function
	// if exactly 0.5, then pass through so we don't get a 1.0
	bvec3 smallcutoff = lessThan(dither, vec3_splat(0.5));
	bvec3 bigcutoff = greaterThan(dither, vec3_splat(0.5));
	dither = mix(dither, dither * vec3_splat(2.0), smallcutoff);
	dither = mix(dither, vec3_splat(2.0) - (dither * vec3_splat(2.0)), bigcutoff);
	// shift down by half
	dither = dither - vec3_splat(0.5);
	// scale down below the specified step size
	dither = dither / vec3_splat(scale_divisor);
	// add to input
	vec3 tempout = saturate(pixelval + dither); // Note: remove this clamping if returning to situation where input (from "NTSC-J to rec2020" conversion) can include values >1.0. Have faith that input was clamped in other cases.

	// Don't dither colors so close to 0 or 1 that dithering would be asymmetric.
	// But do dither values >1.0 in the special case that NTSC-J color correction simulation produces them in HDR mode.
	bvec3 highcutoff = greaterThan(pixelval, vec3_splat(1.0 - (0.5 / scale_divisor)));
	// restore if returning to situation where we need to tolerate input >1.0//bvec3 superhighcutoff = greaterThan(pixelval, vec3_splat(1.0 + (0.5 / scale_divisor)));
	bvec3 lowcutoff = lessThan(pixelval, vec3_splat(0.5 / scale_divisor));
	vec3 outcolor = mix(tempout, pixelval, highcutoff);
	// restore if returning to situation where we need to tolerate input >1.0//outcolor = mix(outcolor, tempout, superhighcutoff);
	outcolor = mix(outcolor, pixelval, lowcutoff);
	return outcolor;
}

// Fog ---------------------------------------------
vec3 ApplyWorldFog(vec3 color, vec3 viewPosition)
{
	float d = sqrt(dot(viewPosition, viewPosition));
	float s0 = 0;
	float e0 = 10000;

	float density = 0.00025;
	float t = 1 / exp (d * density);

	vec3 fogColor0 = vec3(0.1, 0.1, 0.2);
	vec3 outColor = mix(color * fogColor0, color, t);

	float e1 = 15000;
	float t2 = 1.0 - saturate((d - e0) / e1);
	outColor *= t2;

	return outColor;
}

// Spherical world  ---------------------------------------------
#define cplx vec2
#define cplx_new(re, im) vec2(re, im)
#define cplx_re(z) z.x
#define cplx_im(z) z.y
#define cplx_exp(z) (exp(z.x) * cplx_new(cos(z.y), sin(z.y)))
#define cplx_scale(z, scalar) (z * scalar)
#define cplx_abs(z) (sqrt(z.x * z.x + z.y * z.y))

vec3 ApplySphericalWorld(vec3 viewPosition, float radiusScale)
{
	vec3 outResult = vec3(0.0, 0.0, 0.0);

	float rp = -250000 * radiusScale;

	vec2 planedir = normalize(vec2(viewPosition.x, viewPosition.z));
	cplx plane = cplx_new(viewPosition.y, sqrt((viewPosition.x) * (viewPosition.x) + (viewPosition.z) * (viewPosition.z)));
	cplx circle = rp * cplx_exp(cplx_scale(plane, 1.0 / rp)) - cplx_new(rp, 0);
	outResult.x = cplx_im(circle) * planedir.x;
	outResult.z = cplx_im(circle) * planedir.y;
	outResult.y = cplx_re(circle);

	return outResult;
}

#define MAX_BONE_MATRICES 128
